allprojects {}

subprojects {
  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'

  group = 'net.marci.core'
  version = "${marciCoreVersion}"

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(21)
    }
  }

  repositories {
    mavenLocal()
    mavenCentral()
  }

  tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
  }

  artifacts { // This comes with task "sourcesJar"
    archives sourcesJar
  }

  publishing {
    publications {
      "$project.name"(MavenPublication) {
        groupId project.group
        artifactId project.name
        version project.version
        from components.java
        artifact tasks.sourcesJar
      }
    }
  }

  dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation group: 'org.slf4j', name: 'slf4j-api', version: "${slf4jVersion}"
    implementation group: 'jakarta.persistence', name: 'jakarta.persistence-api', version: "${jakartaPersistenceApiVersion}"

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: "${springBootVersion}"
  }

  configurations {
    compileOnly {
      extendsFrom annotationProcessor
    }
  }

  tasks.register("create-dirs") {
    sourceSets*.java.srcDirs*.each { it.mkdirs() }
    sourceSets*.resources.srcDirs*.each { it.mkdirs() }
  }

  tasks.named('test') {
    useJUnitPlatform()
  }
}